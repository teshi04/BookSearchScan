apply plugin: 'com.android.application'
apply plugin: 'android-apt'

android {
    compileSdkVersion 24
    buildToolsVersion '23.0.3'

    dataBinding {
        enabled = true
    }

    defaultConfig {
        applicationId "jp.tsur.booksearch"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 13
        versionName "1.2.8"

        buildConfigField "String", "AWS_ACCESS_KEY", "\"${AWS_ACCESS_KEY}\""
        buildConfigField "String", "AWS_SECRET", "\"${AWS_SECRET}\""
        buildConfigField "String", "ASSOCIATE_TAG", "\"${ASSOCIATE_TAG}\""
    }

    signingConfigs {
        debug

        release
    }

    buildTypes {
        debug

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            if (project.hasProperty('storeFile')) {
                signingConfig signingConfigs.release
            }
        }
    }

    lintOptions {
        checkReleaseBuilds false

    }
    compileOptions {
        encoding = "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    productFlavors {
        develop {
            applicationId "jp.tsur.booksearch.develop"
        }
        production {
            applicationId "jp.tsur.booksearch"
        }
    }
}

if (project.hasProperty('storeFile')) {
    android.signingConfigs.release.storeFile = file(storeFile)
}
if (project.hasProperty('storePassword')) {
    android.signingConfigs.release.storePassword = storePassword
}
if (project.hasProperty('keyAlias')) {
    android.signingConfigs.release.keyAlias = keyAlias
}
if (project.hasProperty('keyPassword')) {
    android.signingConfigs.release.keyPassword = keyPassword
}

dependencies {
    def supportLibraryVersion = '24.2.1'
    def retrofitVersion = '2.1.0'

    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'
    compile "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    compile "com.squareup.retrofit2:adapter-rxjava:${retrofitVersion}"
    compile("com.squareup.retrofit2:converter-simplexml:${retrofitVersion}") {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }
    compile "com.android.support:cardview-v7:${supportLibraryVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibraryVersion}"
    compile "com.android.support:appcompat-v7:${supportLibraryVersion}"
    compile "com.android.support:design:${supportLibraryVersion}"
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.squareup.dagger:dagger:1.2.2'
    apt 'com.squareup.dagger:dagger-compiler:1.2.2'
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'com.google.android.gms:play-services-vision:9.6.1'
    compile 'com.github.hotchemi:permissionsdispatcher:2.2.0'
    apt 'com.github.hotchemi:permissionsdispatcher-processor:2.2.0'
}
