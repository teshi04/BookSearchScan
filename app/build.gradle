apply plugin: 'com.android.application'
apply plugin: 'android-apt'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    defaultConfig {
        applicationId "jp.tsur.booksearch"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 8
        versionName "1.2.3"

        buildConfigField "String", "AWS_ACCESS_KEY", "\"${AWS_ACCESS_KEY}\""
        buildConfigField "String", "AWS_SECRET", "\"${AWS_SECRET}\""
        buildConfigField "String", "ASSOCIATE_TAG", "\"${ASSOCIATE_TAG}\""
    }

    signingConfigs {
        debug

        release
    }

    buildTypes {
        debug

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            if (project.hasProperty('storeFile')) {
                signingConfig signingConfigs.release
            }
        }
    }

    compileOptions {
        encoding = "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    productFlavors {
        develop {
            applicationId "jp.tsur.booksearch.develop"
        }
        production {
            applicationId "jp.tsur.booksearch"
        }
    }
}

if (project.hasProperty('storeFile')) {
    android.signingConfigs.release.storeFile = file(storeFile)
}
if (project.hasProperty('storePassword')) {
    android.signingConfigs.release.storePassword = storePassword
}
if (project.hasProperty('keyAlias')) {
    android.signingConfigs.release.keyAlias = keyAlias
}
if (project.hasProperty('keyPassword')) {
    android.signingConfigs.release.keyPassword = keyPassword
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.jakewharton:butterknife:7.0.1"
    apt "com.jakewharton:butterknife:7.0.1"
    compile 'com.squareup.okhttp3:logging-interceptor:3.1.2'
    compile 'com.squareup.retrofit2:retrofit:2.0.0-beta4'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.0-beta4'
    compile('com.squareup.retrofit2:converter-simplexml:2.0.0-beta4') {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }
    compile "com.android.support:cardview-v7:23.1.1"
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile "com.android.support:appcompat-v7:23.1.1"
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.android.support:design:23.1.1'
    compile 'com.squareup.dagger:dagger:1.2.2'
    apt 'com.squareup.dagger:dagger-compiler:1.2.2'
    compile 'io.reactivex:rxandroid:1.0.1'
}
